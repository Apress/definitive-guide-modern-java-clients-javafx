plugins {
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.8'
    id 'org.hidetake.ssh' version '2.10.1'
}

repositories {
    mavenCentral()
    maven {
        url 'http://nexus.gluonhq.com/nexus/content/repositories/releases/'
    }
}

dependencies {
    implementation 'com.pi4j:pi4j-core:1.2'
    implementation 'com.gluonhq:maps:2.0.0-ea+2'
    implementation 'com.gluonhq.attach:storage:4.0.2:desktop'
    implementation 'com.gluonhq.attach:util:4.0.2'
    implementation 'com.airhacks:afterburner.fx:1.7.0'
    implementation 'javax.annotation:javax.annotation-api:1.3.2'
}

javafx {
    modules = [ 'javafx.controls', 'javafx.fxml' ]
}

mainClassName = "$moduleName/org.modernclients.raspberrypi.gps.MainApp"

jar {
    manifest {
        attributes 'Main-Class': 'org.modernclients.raspberrypi.gps.MainApp'
    }
}

def workingDir = '/home/pi/ModernClients/ch12-RaspberryPi/'
def javaHome = '/opt/jdk-11.0.4+11'
def javafxHome = '/opt/armv6hf-sdk'

task libs(type: Copy) {
    dependsOn 'jar'
    into "${buildDir}/libs/"
    from configurations.runtimeClasspath
}

remotes {
    pi11 {
        host = 'raspberrypi.local'
        user = 'pi'
        password = 'pi'
    }
}

task runRemoteEmbedded {
    dependsOn 'libs'
    ssh.settings {
        knownHosts = allowAnyHosts
    }
    doLast {
        ssh.run {
            session(remotes.pi11) {
                execute "mkdir -p ${workingDir}/${project.name}/dist"

                fileTree("${buildDir}/libs")
                        .filter { it.isFile() && ! it.name.startsWith('javafx')}
                        .files
                        .each { put from:it, into: "${workingDir}/${project.name}/dist/${it.name}"}

                executeSudo "${javaHome}/bin/java -Dfile.encoding=UTF-8 " +
                        "--module-path=${javafxHome}/lib:${workingDir}/${project.name}/dist " +
                        "-Dembedded=monocle -Dglass.platform=Monocle " +
                        "-classpath '${workingDir}/${project.name}/dist/*' " +
                        "-m ${project.mainClassName}"
            }
        }
    }
}

// run only on embedded devices
task runLocalEmbedded(type: Exec) {
    dependsOn 'libs'

    commandLine 'sudo', "${javaHome}/bin/java", '-Dfile.encoding=UTF-8', '--module-path', "${javafxHome}/lib:${buildDir}/libs",
            '-Dembedded=monocle', '-Dglass.platform=Monocle', '-m', "${project.mainClassName}"
}